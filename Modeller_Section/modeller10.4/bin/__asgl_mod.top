# Purpose: Produce an ASGL plot for the principal components analysis.
#
# Input  : TOP variable FAMILY, FAMILY.dat file from __principal_components
#
# Output files: FAMILY.top FAMILY.ps FAMILY.log (ASGL)
#

SUBROUTINE ROUTINE = 'asgl_principal_components'

STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = FAMILY '.dat', ;
               RESULT = FILE

STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = FAMILY '.top', ;
               RESULT = OBJECTS_FILE
OPEN IO_UNIT = 30, FILE_STATUS = 'UNKNOWN', FILE_ACCESS = 'SEQUENTIAL'
# WRITE OBJECTS = '#EPSF'
WRITE OBJECTS = 'READ_TABLE FILE = ' '\''FILE'\''
WRITE OBJECTS = 'SET XY_COLUMNS = 3 4'
WRITE OBJECTS = 'WORLD POSITION = 12 1'
WRITE OBJECTS = 'AXES2D'
WRITE OBJECTS = 'PLOT2D PLOT2D_LINE_TYPE = 0, PLOT2D_SYMBOL_TYPE = 4, ;'
WRITE OBJECTS = '       LABEL_COLUMN = 2, LABEL_LOCATION = 2, LABEL_FONT = 5'
WRITE OBJECTS = 'RESET_CAPTIONS'
STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = 'FAMILY@' FAMILY, ;
               RESULT = FILE
WRITE OBJECTS = 'CAPTION CAPTION_POSITION = 1, ;'
WRITE OBJECTS = '        CAPTION_TEXT = ' '\''FILE'\''
WRITE OBJECTS = 'CAPTION CAPTION_POSITION = 1, ;'
WRITE OBJECTS = '        CAPTION_TEXT = \'PRINCIPAL COMPONENTS CLUSTERING\''
WRITE OBJECTS = 'CAPTION CAPTION_POSITION = 2, CAPTION_TEXT = \'p\''
WRITE OBJECTS = 'CAPTION CAPTION_POSITION = 3, CAPTION_TEXT = \'q\''
CLOSE

STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = 'asgl@' FAMILY, ;
               RESULT = COMMAND
SYSTEM

RETURN
END_SUBROUTINE


#  Purpose: plot the contents of posdif.asgl

SUBROUTINE ROUTINE = 'asgl_posdif'

STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = 'posdif.top', ;
               RESULT = OBJECTS_FILE
OPEN IO_UNIT = 30, FILE_STATUS = 'UNKNOWN', FILE_ACCESS = 'SEQUENTIAL'

# WRITE OBJECTS = '#EPSF'
WRITE OBJECTS = 'READ_TABLE FILE \'posdif.asgl\''
WRITE OBJECTS = 'SET BAR_GRAYNESS = 1, CAPTION_FONT = 8, TICK_FONT = 8'
WRITE OBJECTS = 'SET WORLD_WINDOW 0 -999 -999 -999, XY_COLUMNS 1 2'
WRITE OBJECTS = 'WORLD POSITION 1 2'
WRITE OBJECTS = 'AXES2D'
WRITE OBJECTS = 'HIST2D'
WRITE OBJECTS = 'RESET_CAPTIONS'
WRITE OBJECTS = 'CAPTION CAPTION_POSITION 1, CAPTION_TEXT \'POSITION DIFFERENCE\''
WRITE OBJECTS = 'CAPTION CAPTION_POSITION 2, CAPTION_TEXT \'ALIGNMENT POSITION\''
WRITE OBJECTS = 'CAPTION CAPTION_POSITION 3, CAPTION_TEXT \'@D@C@_a_@ [\305]\''

CLOSE

STRING_OPERATE OPERATION = CONCATENATE, STRING_ARGUMENTS = 'asgl@' 'posdif', ;
               RESULT = COMMAND
SYSTEM

RETURN
END_SUBROUTINE
